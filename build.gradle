import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.21"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.1.2.RELEASE"
    }
}

rootProject.version = "0.0.1"
group = "com.spaceshift"

allprojects {

    apply plugin: "java"
    apply plugin: "groovy"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven { url "https://dl.bintray.com/javasabr/maven" }
        maven { url "https://dl.bintray.com/jasync-sql/jasync-sql" }
    }

    ext {
        annotationVersion = "16.0.3"
        rlibVersion = "9.2.1"
        lombokVersion = '1.18.4'
        springbootVersion = '2.1.4.RELEASE'
        springVersion = '5.1.6.RELEASE'
        junitJupiterVersion = "5.2.0"
        hikariCPVersion = "2.7.8"
        flywayVersion = "5.2.1"
        jsointerVersion = "0.9.23"
        testcontainersVersion = "1.10.1"
        groovyVersion = "2.5.3"
        spockVersion = "1.2-groovy-2.5"
        postgresqlVersion = "42.1.1"
        javassistVersion = "3.24.0-GA"
        jsonwebtokenVersion = "0.10.5"
        hamcrestVersion = "1.3"
        jsonpathVersion = "2.4.0"
        jasyncVersion = "1.0.0"
        nettyVersion = "4.1.36.Final"
        projectReactorVersion = "3.2.11.RELEASE"
    }

    dependencies {
        compileOnly "org.jetbrains:annotations:$annotationVersion"
    }

    afterEvaluate { Project project ->

        if (project.ext.has("lombok") && project.ext["lombok"] == "true") {

            project.apply (plugin: "net.ltgt.apt")
            project.dependencies {
                it.compileOnly "org.projectlombok:lombok:$lombokVersion"
                it.annotationProcessor "org.projectlombok:lombok:$lombokVersion"
            }
        }

        if (project.ext.has("springboot") && project.ext["springboot"] == "true") {
            project.apply (plugin: "org.springframework.boot")
        }
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/groovy'
            }
        }
        test {
            java {
                srcDirs 'src/test/groovy'
            }
        }
    }

    configurations {
        testArtifacts.extendsFrom testRuntime
    }

    task testJar(type: Jar) {
        classifier "test"
        from sourceSets.test.output
    }

    artifacts {
        testArtifacts testJar
    }

    tasks.withType(Test) {
        maxParallelForks = 2
        forkEvery = 100
    }
    
    processResources {
        filter(ReplaceTokens, tokens: [
            
            // USER DB
            'userDbScheme': System.getProperty("userDbScheme", "jcrm-user-db"),
            'userDbHost': System.getProperty("userDbHost", "localhost:5432"),
            'userDbUser': System.getProperty("userDbUser", "postgres"),
            'userDbPassword': System.getProperty("userDbPassword", "root"),

            // DICTIONARY DB
            'dictionaryDbScheme': System.getProperty("dictionaryDbScheme", "jcrm-dictionary-db"),
            'dictionaryDbHost': System.getProperty("dictionaryDbHost", "localhost:5432"),
            'dictionaryDbUser': System.getProperty("dictionaryDbUser", "postgres"),
            'dictionaryDbPassword': System.getProperty("dictionaryDbPassword", "root"),
            
            // SECURITY
            'securityTokenSecretKey': System.getProperty("securityTokenSecretKey", "7e9f81de588c09edd3f2dd5e3a0397344b659b1aa54024cfde1fa2527ffe57818c059b2a1df89b52427e06a60714093609fa9087414d42c779e71822fec33079"),
            
            // EMAL
            'mailUser': System.getProperty("mailUser", ""),
            'mailPassword': System.getProperty("mailPassword", ""),
            'mailFrom': System.getProperty("mailFrom", ""),
            
            //SSL
            'keystorePath': System.getProperty("keystorePath", "/opt/jcrm/dev.p12"),
            'keystorePassword': System.getProperty("keystorePassword", "devdevdev"),
            'keystoreAlias': System.getProperty("keystoreAlias", "dev"),
        ])
    }
}

task buildSingleArtifact(type: GradleBuild) {
    group = 'build'
    description = 'Build all modules -> Build result artifact'
    tasks = ['build', 'bootJar']
}

task buildSingleArtifactWithoutTests(type: GradleBuild) {
    group = 'build'
    description = 'Build all modules without tests -> Build result artifact'
    tasks = ['classes', 'bootJar']
}

wrapper {
    gradleVersion = '5.4.1'
    distributionType = Wrapper.DistributionType.ALL
}
