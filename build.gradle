
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.19"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.1.2.RELEASE"
    }
}

rootProject.version = "0.0.1"
group = "com.spaceshift"

allprojects {

    apply plugin: "java"
    apply plugin: "groovy"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven { url "https://dl.bintray.com/javasabr/maven" }
    }

    ext {
        annotationVersion = "16.0.3"
        rlibVersion = "9.1.0"
        lombokVersion = '1.18.4'
        lombokGradlePluginVersion = '0.19'
        springbootVersion = '2.1.3.RELEASE'
        springVersion = '5.1.5.RELEASE'
        junitJupiterVersion = "5.2.0"
        hikariCPVersion = "2.7.8"
        flywayVersion = "5.2.1"
        jsointerVersion = "0.9.23"
        testcontainersVersion = "1.10.1"
        groovyVersion = "2.5.3"
        spockVersion = "1.2-groovy-2.5"
        postgresqlVersion = "42.1.1"
        javassistVersion = "3.24.0-GA"
        jsonwebtokenVersion = "0.10.5"
        hamcrestVersion = "1.3"
        jsonpathVersion = "2.4.0"
        javaxMailVersion = "1.5.0-b01"
    }

    dependencies {
        compileOnly "org.jetbrains:annotations:$annotationVersion"
    }

    afterEvaluate { Project project ->

        if (project.ext.has("lombok") && project.ext["lombok"] == "true") {

            project.apply (plugin: "net.ltgt.apt")
            project.dependencies {
                it.compileOnly "org.projectlombok:lombok:$lombokVersion"
                it.annotationProcessor "org.projectlombok:lombok:$lombokVersion"
            }
        }

        if (project.ext.has("springboot") && project.ext["springboot"] == "true") {
            project.apply (plugin: "org.springframework.boot")
        }
    }
    
    test {
        testLogging {
            showStandardStreams = true
        }
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/groovy'
            }
        }
        test {
            java {
                srcDirs 'src/test/groovy'
            }
        }
    }

    configurations {
        testArtifacts.extendsFrom testRuntime
    }

    task testJar(type: Jar) {
        classifier "test"
        from sourceSets.test.output
    }

    artifacts {
        testArtifacts testJar
    }

    tasks.withType(Test) {
        maxParallelForks = Runtime.runtime.availableProcessors()
        forkEvery = 100
    }
}

wrapper {
    gradleVersion = '5.3.1'
    distributionType = Wrapper.DistributionType.ALL
}