import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.2.4.RELEASE"
    }
}

rootProject.version = "0.0.2"
group = "com.spaceshift"

allprojects {

    apply plugin: "java"
    apply plugin: "groovy"

    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14

    repositories {
        jcenter()
        mavenCentral()
        mavenLocal()
        maven { url "https://dl.bintray.com/javasabr/maven" }
        maven { url "https://dl.bintray.com/jasync-sql/jasync-sql" }
    }

    ext {
        angularVersion = "9.1.7"
        annotationVersion = "19.0.0"
        rlibVersion = "9.9.0"
        lombokVersion = '1.18.12'
        springbootVersion = '2.3.0.RELEASE'
        springVersion = '5.2.6.RELEASE'
        junitJupiterVersion = "5.6.2"
        hikariCPVersion = "2.7.8"
        flywayVersion = "6.4.2"
        testcontainersVersion = "1.14.3"
        groovyVersion = "3.0.4"
        spockVersion = "2.0-M2-groovy-3.0"
        postgresqlVersion = "42.1.1"
        javassistVersion = "3.27.0-GA"
        jsonwebtokenVersion = "0.11.1"
        hamcrestVersion = "1.3"
        jsonpathVersion = "2.4.0"
        jasyncVersion = "1.1.0"
        projectReactorVersion = "3.3.5.RELEASE"
        jacksonVersion = "2.11.0"
    }

    dependencies {
        compileOnly "org.jetbrains:annotations:$annotationVersion"
    }

    afterEvaluate { Project project ->

        if (project.ext.has("lombok") && project.ext["lombok"] == "true") {
            project.dependencies {
                it.compileOnly "org.projectlombok:lombok:$lombokVersion"
                it.annotationProcessor "org.projectlombok:lombok:$lombokVersion"
            }
        }

        if (project.ext.has("springboot") && project.ext["springboot"] == "true") {
            project.apply (plugin: "org.springframework.boot")
        }
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/groovy'
            }
        }
        test {
            java {
                srcDirs 'src/test/groovy'
            }
        }
    }

    configurations {
        testArtifacts.extendsFrom testRuntime
    }

    task testJar(type: Jar) {
        classifier "test"
        from sourceSets.test.output
    }

    artifacts {
        testArtifacts testJar
    }
    
    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
    
    tasks.withType(Test) {
        maxParallelForks = 2
        forkEvery = 100
        jvmArgs += "--enable-preview"
    }
    
    tasks.withType(JavaCompile) {
        options.compilerArgs += "--enable-preview"
    }
    
    tasks.withType(GroovyCompile) {
        options.forkOptions.jvmArgs += "--enable-preview"
    }
    
    processResources {
        filter(ReplaceTokens, tokens: [
            
            // USER DB
            'userDbScheme': System.getProperty("userDbScheme", "jcrm-user-db"),
            'userDbHost': System.getProperty("userDbHost", "localhost"),
            'userDbPort': System.getProperty("userDbPort", "5432"),
            'userDbDatabase': System.getProperty("userDbDatabase", "jcrm"),
            'userDbUser': System.getProperty("userDbUser", "postgres"),
            'userDbPassword': System.getProperty("userDbPassword", "root"),

            // DICTIONARY DB
            'dictionaryDbScheme': System.getProperty("dictionaryDbScheme", "jcrm-dictionary-db"),
            'dictionaryDbHost': System.getProperty("dictionaryDbHost", "localhost"),
            'dictionaryDbPort': System.getProperty("dictionaryDbPort", "5432"),
            'dictionaryDbDatabase': System.getProperty("dictionaryDbDatabase", "jcrm"),
            'dictionaryDbUser': System.getProperty("dictionaryDbUser", "postgres"),
            'dictionaryDbPassword': System.getProperty("dictionaryDbPassword", "root"),

            // CLIENT DB
            'clientDbScheme': System.getProperty("clientDbScheme", "jcrm-client-db"),
            'clientDbHost': System.getProperty("clientDbHost", "localhost"),
            'clientDbPort': System.getProperty("clientDbPort", "5432"),
            'clientDbDatabase': System.getProperty("clientDbDatabase", "jcrm"),
            'clientDbUser': System.getProperty("clientDbUser", "postgres"),
            'clientDbPassword': System.getProperty("clientDbPassword", "root"),
            
            // SECURITY
            'securityTokenSecretKey': System.getProperty("securityTokenSecretKey", "7e9f81de588c09edd3f2dd5e3a0397344b659b1aa54024cfde1fa2527ffe57818c059b2a1df89b52427e06a60714093609fa9087414d42c779e71822fec33079"),
            
            // EMAL
            'mailUser': System.getProperty("mailUser", ""),
            'mailPassword': System.getProperty("mailPassword", ""),
            'mailFrom': System.getProperty("mailFrom", ""),
            
            // SSL
            'keystorePath': System.getProperty("keystorePath", projectDir.toPath().resolve("../jcrm.p12").toString()),
            'keystorePassword': System.getProperty("keystorePassword", "ssljcrm"),
            'keystoreAlias': System.getProperty("keystoreAlias", "jcrm"),
        ])
    }
}

task buildSingleArtifact(type: GradleBuild) {
    group = 'build'
    description = 'Build all modules -> Build result artifact'
    tasks = ['build', 'bootJar']
}

task buildSingleArtifactWithoutTests(type: GradleBuild) {
    group = 'build'
    description = 'Build all modules without tests -> Build result artifact'
    tasks = ['classes', 'bootJar']
}

wrapper {
    gradleVersion = '6.4.1'
    distributionType = Wrapper.DistributionType.ALL
}
