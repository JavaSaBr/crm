
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.19"
    }
}

rootProject.version = "0.0.1"
group = "com.spaceshift"

allprojects {

    apply plugin: "java"
    apply plugin: "groovy"
    apply plugin: "net.ltgt.apt"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        jcenter()
        mavenCentral()
        maven { url  "https://dl.bintray.com/javasabr/maven" }
    }

    ext {
        annotationVersion = "16.0.3"
        rlibVersion = "8.1.0"
        lombokVersion = '1.18.4'
        lombokGradlePluginVersion = '0.19'
        springbootVersion = '2.1.0.RELEASE'
        springVersion = '5.1.2.RELEASE'
        junitJupiterVersion = "5.2.0"
        hikariCPVersion = "2.7.8"
        flywayVersion = "5.2.1"
        jsointerVersion = "0.9.23"
        testcontainersVersion = "1.10.1"
        groovyVersion = "2.5.3"
        spockVersion = "1.2-groovy-2.5"
        postgresqlVersion = "42.1.1"
        javassistVersion = "3.24.0-GA"
        jsonwebtokenVersion = "0.10.5"
    }

    dependencies {
        compileOnly "org.jetbrains:annotations:$annotationVersion"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    }

    sourceSets {
        main {
            java {
                srcDirs 'src/main/groovy'
            }
        }
        test {
            java {
                srcDirs 'src/test/groovy'
            }
        }
    }

    configurations {
        testArtifacts.extendsFrom testRuntime
    }

    task testJar(type: Jar) {
        classifier "test"
        from sourceSets.test.output
    }

    artifacts {
        testArtifacts testJar
    }
}

wrapper {
    gradleVersion = '4.10'
    distributionType = Wrapper.DistributionType.ALL
}