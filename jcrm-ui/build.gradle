task pullAngularCliImage(type: Exec, group: 'Angular') {
    
    def useDocker = Boolean.parseBoolean(System.getProperty("useNpmDocker", "true")) ||
        Boolean.parseBoolean(System.getProperty("useAngularDocker", "true"))
    
    if (useDocker) {
        commandLine 'docker', 'pull', 'trion/ng-cli'
    } else {
        commandLine 'echo'
    }
    
    println "Need to pull angular docker:$useDocker"
}

task installNodeModules(type: Exec, group: 'Angular') {
    
    def useDocker = Boolean.parseBoolean(System.getProperty("useNpmDocker", "true"))
    
    if (useDocker) {
        commandLine 'docker', 'run', '-u', (getDockerUserId() + ":" + getDockerGroupId()), '--rm', '-v',
            getDockerMappingFolder(), 'trion/ng-cli', 'npm', 'install'
    } else {
        commandLine 'npm', 'install'
    }
    
    println "Install node modules using docker:$useDocker with args:$commandLine"
}

task updateAngular(type: Exec, group: 'Angular') {
    
    def useDocker = Boolean.parseBoolean(System.getProperty("useAngularDocker", "true"))
    
    if (useDocker) {
        commandLine 'docker', 'run', '-u', "${getDockerUserId()}:${getDockerGroupId()}", '--rm', '-v',
            getDockerMappingFolder(), 'trion/ng-cli', 'ng', 'update', '--all'
    } else {
        commandLine 'ng', 'update', '--all'
    }
    
    println "Update angular using docker:$useDocker with args:$commandLine"
}

task buildAngular(type: Exec, group: 'Build') {
    
    def useDocker = Boolean.parseBoolean(System.getProperty("useAngularDocker", "true"))
    def env = System.getProperty("angular.env", "local")
    
    if (useDocker) {
        commandLine 'docker', 'run', '-u', "${getDockerUserId()}:${getDockerGroupId()}", '--rm', '-v',
            getDockerMappingFolder(), 'trion/ng-cli', 'ng', 'build', "--configuration=$env"
    } else {
        commandLine 'ng', 'build', "--configuration=$env"
    }
    
    println "Build angular using docker:$useDocker with args:$commandLine"
}

task debug(type: Exec, group: 'Application', description: 'Debug front-end') {
    commandLine 'docker', 'run', '-u', "${getDockerUserId()}:${getDockerGroupId()}", '--rm', '-p', '4200:4200', '-v',
        getDockerMappingFolder(), 'trion/ng-cli', 'ng', 'serve', "--configuration=local"
}

static def getDockerUserId() {
    return System.getProperty("docker.user.id", "1000")
}

static def getDockerGroupId() {
    return System.getProperty("docker.group.id", "1000")
}

sourceSets {
    main {
        java {
            srcDirs 'src'
        }
    }
}

tasks.installNodeModules.dependsOn pullAngularCliImage
tasks.buildAngular.dependsOn installNodeModules
tasks.classes.dependsOn buildAngular

def getDockerMappingFolder() {
    return project.projectDir.path + ":/app"
}
